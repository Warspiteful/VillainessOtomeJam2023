init python:
    LEWIS = 0
    CENRIC = 0


label start:

    play music music_Credits
    show cg1

    "The hit manhwa 'My Magical Kindness Made the Cold Prince Fall in Love!!' is the newest craze among manhwa readers."
    "Honestly…it's not the greatest out there. I've definitely read better. The plot is repetitive and the characters are the most basic tropes."
    "However…there's one thing that keeps me reading: the food."
    y "Ahh! I just love how the desserts look so realistic!"
    y "I know Otillia and Lewis are having a moment, but…"
    "I sigh, scrolling down my phone screen."
    y "That strawberry shortcake… I want to reach through the screen and grab it."
    "My stomach growls and I sigh once again."
    "It's late at night, as it usually is when I do my daily reading of manhwa."
    y "Maybe I should get a snack."
    y "Just a few more chapters. Then I will."
    "As I read, I can feel myself getting drowsy."
    "The pale blue light makes my eyes tired. I try to keep myself awake, but my body feels weird."
    y "Ugh…"
    "I lay back on my bed, phone in hand. My body feels light and tingly."
    y "(Did I eat today? I'm sure I did.)"
    y "(So why…do I feel this way?)"
    stop music fadeout 1.0
    "My eyes drift close. I decide not to fight the feeling and fall asleep. I'll just eat a big breakfast tomorrow."
    scene black with fade
    call screen custom_input_modal("What was the villainess's name?", 'mcname')

    scene bg mcroom base with fade
    play music music_Main
    "The next morning, I could hear the sounds of birds chirping. My nose twitches as a sweet scent slowly wafts through the room."
    "With a groan, I sit up, my eyes still closed. My muscles are sore and it feels as if I ran a marathon."
    "The smell of food is enough to make me crack open my eyes. Immediately, I notice a silver tray on my bedside."
    show d oh with dissolve
    mc "What's this?"
    "I pick it up and set it on my lap. With a thick swallow, I take off the lid."
    show d fc
    mc "Oh. My. God."
    "On the tray is a delectable looking breakfast spread. Two scones, a tiny container of jam, a croissant, and even a bagel with cream cheese spread across it… My mouth is already watering."
    mc "Don't mind if I do!"
    "Without a second thought, I start digging in."
    scene cg2 with dissolve
    "I let out a satisfied groan as I eat."
    mc "This is delicious!!"
    "Whoever made this deserves the world. It's absolutely perfect."
    "My thoughts are interrupted by a lady bursting into my room. I pause mid bite."
    scene bg mcroom base
    show d oh at left
    show maid at right, flip
    with dissolve
    "She stares at me."
    "I stare back."
    "She keeps staring."
    m "My lady?"

    "It’s not until that moment when my surroundings finally start coming into view."
    "An expensive wardrobe with a mirror gilded in gold. Expensive white trim detailing the entire room. Floral wallpaper. Thick and soft down comforter."
    "I am not in my bedroom."
    "I take another bite of my croissant, my eyebrows furrowing. The lady in front of me, who I now recognize as a maid, looks at me in concern."
    show d neu
    mc "Hmmm…"
    "I keep looking around. An odd sense of familiarity settles in my stomach."
    show d oh
    "Something about this room…I know it."
    "I finish my croissant and then look over at the maid."
    show d ss
    mc "Can you hand me a mirror?"
    "She bows her head."
    m "Yes, my lady."
    "She seems nervous as she grabs a handheld mirror off the dressing table. She hands it to me, not making eye contact."
    show d oh
    "Clue number one."
    show d ls
    "I look in the mirror. I'm met with rosy and plump cheeks. My hair is short, brown, and curly."
    "Clue number two."
    show d oh
    mc "Ah. So it does happen."
    "Isekai: A genre where the main character gets transported to another world, usually through death."
    show d gloom
    mc "(I don't remember dying. I only remember falling asleep.)"
    mc "Perhaps…"
    show d a
    "I mutter to myself. I frown. There is a hint of disappointment. If I was going to be isekai'd…"
    mc "(I wanted to be hit by a truck.)"
    "Well, you can't pick and choose."
    show d neu
    "I set the mirror down next to me and pick up one of the scones. I slather a healthy amount of jelly on it and start to eat."
    "It would seem that I am now living in the world of 'My Magical Kindness Made the Cold Prince Fall in Love!!' Not my first choice."
    "I'm in the role of villainess. Also not ideal. In the many, many manhwa I've consumed, the villainess never gets the happy ending."
    show d pout sad
    "I pout, taking another bite of my food. I look over at the maid."
    show d ss
    mc "Where are we?"
    m "Excuse me?"
    show d neu
    "I speak again."
    mc "Where are we?"
    "The maid tries and fails to not look too concerned."
    m "The castle…"
    show d oh
    mc "Oh!"
    "My eyes widen as I pinpoint where in the story I was transported to."
    "My character, the villainess, is engaged to the love interest, Lewis. The heroine of the story, Otillia, has just started to study at the castle."
    show d pout sad
    "I sigh to myself, finishing my food."
    show d neu
    m "Are… Are you ready to be dressed, my lady?"
    "I nod and set the tray on the bed. I get up and get dressed with the maid's help."
    m "My lady…are you all right?"
    show d oh
    mc "Hmmm?"
    "I look over at her."
    show d n b
    mc "Oh, yes. I suppose breakfast has me feeling sluggish."
    m "The food wasn't to your liking again? I can inform–"
    show d ss
    mc "That's not it."
    "I wave her off, giving her a smile."
    show d pout sad
    mc "I guess I'm left wanting more."
    show d ss
    "I decide not to dwell on it. After I'm dressed, the maid leads me out into the common room."
    "She says nothing more, but it's obvious that she doesn't trust my word."
    scene bg tearoom day with fade
    "The common room is just as elaborate as my bedroom, if not more." #WIP: [mcname]
    "The first thing I notice is a large bookshelf sprawling the length of one wall. A heavy dark oak table is surrounded by velvet couches and chairs."
    "Behind it are windows with thick red curtains that have been opened to let in the morning light."
    "A man sits in the padded chair at the head of the table. There's food laid out in front of him. All of it untouched."
    scene cg3 with dissolve
    "Lewis, the love interest of the manhwa."
    "He was popular among readers due to his older age. And I have to say that he is much more handsome in real life."
    "A soft complexion, long hair with gray streaks, glasses that rest gently on his nose, and tied together with a stern expression…"
    "Megane: A character with glasses. Oftentimes, male megane are seen as stoic and aloof."
    scene bg tearoom day
    show d n b at left
    show l neu at right, flip
    with dissolve
    stop music fadeout 3.0
    play music music_Lewis_nm fadein 3.0
    voice l1
    l "Oh, you're awake. Good morning."
    "I just stare at him. It takes me a minute to realize he's talking to me."
    show d sb
    mc "G-good morning…"
    show d oh
    "Wait, how do I address him?"
    "Sir? My lord? Prince?"
    "I try to remember how the villainess addressed him in the manhwa."
    show d ss
    mc "Lewis."
    show l nerve
    voice l2
    voice l_sigh
    l "All right then…"
    "He clears his throat awkwardly."
    show l neu
    voice l3
    l "Do sit."
    show d neu
    mc "Right."
    "I sit across from him. My eyes keep flickering from him to the food in front of me."
    show d fc
    "There is even more delicious looking food than what I got this morning. I\’m practically drooling once again."
    "Lewis picks up a newspaper and starts reading. He only stops to drink coffee from his cup."
    show d fbb
    "All that food…untouched."
    "I can’t hold back anymore."
    show d n b
    mc "Are you hungry?"
    "Lewis lowers his paper, his eyebrow raised."
    show l nerve
    l "Hmm?"
    mc "You’re not eating."
    "Lewis gives me an odd look before clearing his throat."
    show l neu
    l "I am not hungry."
    menu:
        "Then can I eat?":
            $CENRIC += 1
            jump choice1_eat
        "You need to eat breakfast. It's the most important meal of the day.":
            $LEWIS += 1
            jump choice1_important


label choice1_eat:
    show l shock
    voice l_this
    "Lewis gives me an even weirder look."
    "It's as if I’d grown two heads."
    show d sb
    mc "What is it? Why are you looking at me like that?"
    l "Because you never cared for food before."
    show d oh
    "I blink."
    "That’s right. I’m not me anymore. I’m the villainess."
    "But food is food."
    show d ss
    "I shrug and give him a smile."
    mc "I've simply had a change of heart."
    show l an
    voice l_sigh
    "Lewis sighs, going back to his newspaper."
    l "Go ahead. I do not mind."
    show d ls
    mc "Yay!!"
    "I take a croissant, nibbling happily on it."
    jump choice1_done


label choice1_important:
    show l nerve
    l "And…why do you care?"
    show d oh
    "The question catches me off guard. It’s not said in a rude way–it's more genuine confusion."
    show d ss
    mc "We’re engaged, aren’t we? As your fiance, I need to make sure you’re taking care of yourself."
    "Lewis stares at me. And he keeps staring."
    "I stare back."
    show l neu
    voice l_sigh
    "After a moment, he just shakes his head and sighs."
    show d pout upset
    mc "I’m guessing that’s a no?"
    voice l_ofc
    l "You’d guess right."
    "I pout slightly and lean forward, taking a croissant in my hand."
    mc "Suit yourself."
    jump choice1_done


label choice1_done:
    scene bg tearoom day
    show d neu at center with dissolve
    "Lewis sips on his coffee, not paying me any attention. I use the time to think about how I should go about this whole villainess thing."
    "The manhwa was fairly new and I had read all available episodes."
    show d gloom
    "When [mcname] showed up in the story, she was cruel from the get go. Stubborn, violent, and selfish, she was hated in the fandom."
    "I don't want that. And I don't want whatever death flags the author had planned."
    "Death flags: An element in a story used to foreshadow a character's imminent death or downfall."
    show d pout sad
    "Was this even worth it?"
    show d oh
    "I think back on my past life. It wasn't anything special."
    "Though, I suppose the criteria for being isekai'd is being unremarkable and then starting a new life."
    "I am…was a normal office worker. Not a manager, just someone who stayed mediocre."
    show d ga
    "Despite that, I was overworked. I found myself eating mediocre instant ramen from the convenience store."
    "At night, I would get my fill of delicious food while reading manhwa and manga."
    show d gloom
    "Now that I think back on it, my life was kind of pathetic. Sure, I enjoyed the nights I had, but my life as a whole wasn't fulfilling."
    show d ls
    "I look down at the table, the plates cleared of food and my stomach happy."
    show d fc
    mc "(Yeah. This is way better.)"
    l "[mcname]."
    "Here, I can live for myself. I can eat all the delicious food and not be tied down by anything."
    show d fbb
    "Not even the title of villainess. Although it would be a shame if I die again because of it."
    "I sigh happily and pick up a cream puff."
    show l neu at right, flip
    show d oh at left
    with dissolve
    with move
    l "[mcname]!"
    "I'm knocked out of my thoughts as I look up at Lewis."
    mc "Sorry, sorry. What is it?"
    show l nerve
    voice l_sigh
    "His eyebrows furrow once again as he leans forward."
    voice l4
    l "You've been acting odd."
    show d gloom
    mc "Odd?"
    voice l5
    l "The [mcname] I know would currently be talking about the gossip she’s been hearing around the castle."
    show d neu
    "I shake my head slightly and wave my hand dismissively."
    show d ss
    mc "I'm not interested in petty gossip."
    stop music
    show d sb
    show l shock
    "The room is filled with gasps. I look around, confused."
    "The castle staff and Lewis are all staring at me in shock."
    show d oh
    mc "Did I… Did I say something wrong?"
    show maid at offscreenright
    show maid at center with move #Maid Sprite
    show l shock
    "The maid shakily points at me, her face filled with fear."
    m "Who are you and what have you done with [mcname]?"
    play music music_Main_nm
    show d gloom
    "Oh, right."
    "[mcname] loved gossip and spreading rumors. It eventually made a rift between her and Lewis."
    show d oh
    "I quickly try to cover my words."
    "Before I can speak, the maid talks once again."
    show d gloom
    m "My lord, she's been acting odd since she woke up. She's been eating food she normally doesn't and has been acting more…"
    "She looks over at me, as if fearful of my reaction."
    show d ss
    m "More docile."
    show l an
    voice l_sigh
    "Lewis sighs and pinches the bridge of his nose."
    voice l6
    l "I do not have time for this."
    show l a
    "He scowls at me."
    voice l7
    l "Otillia is visiting soon. Be ready by then."
    show l at offscreenright
    show maid at right, flip
    with move
    "Without another word, he gets up and exits the room."
    "I turn towards the maid, the uneaten cream puff in my hand."
    show d fbb
    mc "Could you possibly ask the chef if there are any more croissants? They were delicious."
    m "Yes, my lady."
    hide maid
    show d neu at center
    with dissolve
    "She stares at me one last time before making her way to the kitchen. I start eating the cream puff, letting my thoughts come together."
    "In the manhwa, Otillia and [mcname]'s meeting was basically the start of the main plotline."
    "The original [mcname] was rude towards Otillia, despite her prowess in magic and being personally trained by the head mage."
    "She laughed in Otillia's face and made fun of her aspirations."
    show d gloom
    mc "(Classic villainess.)"
    "Lewis ended up defending Otillia. She was touched and that's when they started to get closer."
    show d oh
    mc "If I can just avoid this first flag…I should be in the clear."
    "It's not like I don't want them to end up together. I just want to live and not suffer the fate of every manhwa villainess."
    show d neu
    "When I'm about to formulate a plan, I hear hushed whispering from the corner."
    play music music_Cenric_nm
    scene cg4 with dissolve
    "I look towards the source of the noise and see the maid and a man in a chef's uniform conversing with each other."
    "The man is tall and big, with fluffy blonde hair and a slightly chubby face."
    "Cute."
    "With a confused and slightly concerned smile, I nod in their direction."
    "Both of them walk over to me. The chef bows deeply, not making eye contact."
    scene bg tearoom day
    show d neu at left
    show c puppy lookDown at right, flip
    voice c1

    c "My lady… I'm Cenric. The head chef."
    menu:
        "Your food is absolutely delicious!":
            $CENRIC += 1
            jump choice2_delicious
        "It's a pleasure to meet you, Cenric!":
            $LEWIS += 1
            jump choice2_pleasure


label choice2_delicious:
    show c smile lookDown
    "Cenric straightens, his face lit up in a smile. His cheeks are a dark red."
    voice c2
    c "Th-thank you, my lady."
    show d bb
    mc "(He looks like a cute golden retriever.)"
    show d ss
    "Golden Retriever: A character that highly resembles a golden retriever. Often kind, blond, and a bit stupid."
    voice c3
    c "I w-wanted to personally see what else you would like to eat."
    voice c4
    c "I know you said you wanted croissants but…I can make you anything if you please."
    show d fc
    "My eyes immediately shine at the thought. As soon as I open my mouth, a flood of requests starts pouring out."
    mc "Anything? Can you make doughnuts? Crepes? Curry? Hamburg steak? Stew? Parfaits? Oooh! There's so many options!!"
    show c flust surp
    "Cenric looks surprised but smiles happily."
    voice c5
    c "Anything you wish, my lady."
    show c smile lookDown
    "I take a second to think."
    show d oh
    "This is a big decision. The ultimate power: the ability to have any type of food made for me."
    show d pout upset
    "Well, mostly any type. I doubt I could get something like tteokbokki here."
    show d oh
    "I gasp as I suddenly think of one of my favorite comfort foods."
    show d ls
    mc "Since Otillia is visiting, can you make some tea and foods to go along with it? And be sure to include strawberry shortcake."
    voice c6
    c "As you wish, my lady."
    hide c with dissolve
    show d ss at center with move
    "Cenric bows his head, that same goofy grin on his face. He turns and makes his way back to the kitchen."
    "I smile and mutter to myself."
    show d ls
    mc "So adorable."
    jump choice2_done

label choice2_pleasure:
    voice c7
    c "It's rare for someone to come back for more food."
    "He still avoids eye contact, bowing deeply."
    show d ss
    "I give him a gentle smile."
    mc "It was quite delicious. I wanted you to make some more so I can share with Lewis when he gets back."
    show c smile neu
    voice c8
    c "Do you have anything in mind?"
    show d oh
    "I think for a second. If Otillia will also be visiting, it would be wise to set an inviting atmosphere."
    "This could work."
    show d ss
    mc "Well, since Otillia is also visiting, could you please prepare tea and some finger foods for us?"
    voice c9
    c "Any specifics?"
    show d oh
    mc "Hmmm…"
    show d gloom
    "For some reason, the sight of Lewis not eating fills my mind."
    show d ls
    mc "Something that Lewis would like."
    voice c10
    show c close blush smile
    c "Of course, my lady."
    show c smile neu at offscreenright
    show d neu at center with move
    "With another bow, he makes his way back to the kitchen."


label choice2_done:
    show d oh
    "Since I'm alone–"
    show d gloom
    "I look over to the maid standing in the corner."
    show d oh
    "Well, relatively alone, I'll take this time to look around."
    show d ss
    "It's so odd and very surreal being in a place that was fictional before."
    show d sb
    "I walk around the tea room, taking in every bit of detail."
    "Fantasy manhwa tend to have a lot of detail and 'My Magical Kindness Made the Cold Prince Fall in Love!!' was no exception."
    show d oh
    "Everything is elegant and just screams royalty. Not to mention…glittery. Why is everything so glittery?"
    show d gloom
    mc "It's like I'll be blinded."
    show d ls
    "I touch a vase with gold accents, watching as it glints in the sunlight."
    "To think I would ever end up in a place like this. It makes me wonder about my old life."
    show d oh
    "Did people even know that I died? Would they even care? What about–"
    show d gloom
    mc "What about the food in my fridge? It'll spoil. Such a waste."
    play music music_Oti_nm
    scene cg5 with dissolve
    "I can hear more voices getting closer to the room. I turn and see Lewis, Otillia, and the grand mage."
    mc "Woah."
    "Otillia is even more gorgeous in person. Golden locks fall across her shoulders, looking as smooth as silk. She has a rosy complexion, her hazel eyes captivating."
    "She's the typical heroine, except her beauty puts her above others. This is when it sets in that I truly am inside a manhwa."
    play music music_Main_nm
    scene bg tearoom day
    show d ss at center
    show o neu at right, flip
    show d ls at left
    with dissolve
    "I grin, walking over to them. I curtsy, my eyes meeting hers."
    mc "It's a pleasure to meet you, Miss Otillia. I am Lady [mcname], but please, call me [mcname]."
    show o flust
    voice o1
    o "O-oh!"
    "Otillia's eyes widen. Just for a second, she hesitates before returning the curtsy."
    show o smile nor
    show d gloom
    "It must be a surprise. [mcname]'s reputation was already less than ideal from the get go."
    "Besides the rumors she spread, she was known to be mean-spirited."
    "What's supposed to happen at this point is that [mcname]…or I am supposed to scoff and make fun of her background."
    "I call her a wannabe noble that has no true talent and will only end up as trash."
    "Lewis would then immediately come to Otillia's aid and scold [mcname], calling her bitter and jealous."
    show d pout upset
    mc "(Yikes.)"
    show d gloom
    "Since I don't want to see my doom, I take the obvious route."
    show d ss
    mc "Thank you so much for coming over!! I've heard all about your gift, Miss Otillia."
    show o ce happy
    "Otillia smiles, albeit awkwardly."
    voice o2
    o "It's a pleasure to meet you as well, Lady [mcname]."
    show d oh
    show o neu
    mc "Oh, please. Call me [mcname]. It's fine, I promise."
    mc "Come, sit."
    show d ss
    show o neu at center
    with move
    show l neu at right, flip
    with dissolve
    "I guide her over to the couch. We both sit as the grand mage and Lewis sit across from us. I take a deep breath. Doing good so far."
    mc "Please, tell me about your studies so far. I have never met someone with powerful magic before."
    show o flust smile
    "Otillia smiles, looking a bit cautious. It's best if I'm not too enthusiastic or it would seem like I'm scheming up a plan."
    voice o_alright
    o "Oh, it's not much. It's just light magic. Healing… I'm not special."
    "The grand mage shakes her head."
    show l neu at right, flip
    gm "Light magic is the rarest form of magic. Not to mention your healing powers are quite exceptional. You are like a gift from the gods."
    show o flust ce smile
    "Despite looking uncomfortable, Otillia blushes, looking down into her lap."
    voice o_laugh
    o "I wouldn't say that much."
    show l shock
    l "Healing magic, you say?"
    "I look over at Lewis. In the story, he had Otillia attempt to heal his brother. Unfortunately, the disease he had was already too far along, and Lewis ended up taking the throne."
    o "I've been practicing, but it still drains a lot out of me. I won't be useful for a while."
    show l ss
    show d n l
    "I shake my head, laughing slightly."
    mc "That's all right. I don't think anyone is in a rush to have you go out and do some healing."
    show o smile nor
    "She seems to finally relax as she nods."
    voice o_glad
    o "Thank you for saying that, my lady."
    show l nerve
    hide o with dissolve
    l "By the way, I wanted to ask about–"
    show c conf at center
    show d oh
    show l shock
    with moveinright
    "Lewis's words are interrupted by Cenric entering the room. He's rolling a cart filled with tea and snacks. My eyes immediately light up."
    voice l_sigh
    "Lewis looks surprised before sighing and glancing over at me."
    l "[mcname], what is this?"
    show d fc
    mc "I asked Cenric to make us some snacks!"
    show l an
    "I squeal, standing up as Cenric positions the cart near the table. Lewis' expression turns into a scowl."
    show l a
    voice l_stop
    l "Good lord, [mcname]. This is practically enough for a party! Why did you do this?"
    menu:
        "Because I wanted to eat more of Cenric's cooking.":
            $CENRIC += 1
            jump choice3_eatmore
        "You need to appreciate Cenric's cooking more.":
            $LEWIS += 1
            jump choice3_appreciate

label choice3_eatmore:
    show c smile lookDown
    "Cenric blushes and then bows his head."
    voice c_aww
    c "You're too kind, my lady."
    show d ls
    mc "It's true!"
    "I then motion towards Otillia."
    show d fc
    mc "Come! You must taste his cooking. It's absolutely divine."
    show l an
    voice l_stop
    l "I don't think it's entirely appropriate–"
    show d pout upset
    mc "Oh, shush. We have guests. We can't have them hungry."
    show c at right
    show l at offscreenright
    show d at left
    show o flust at center
    with moveinright
    hide l
    "Otillia walks over, her eyes scanning all the options."
    show d ls
    mc "Pick whatever you'd like."
    show o blush smile
    "She settles on a pink macaron filled with some type of jelly. She takes a small bite. Then she takes a bigger one, popping the whole thing in her mouth."
    show o happy ex
    voice o_laugh
    o "This is delicious!"
    show c blush laugh
    voice c_laugh
    "Cenric chuckles, rubbing the back of his head sheepishly."
    c "It's not my best, but I appreciate your words."
    show d a
    show c huh blush
    "I click my tongue, giving him a mock stern look."
    mc "You need to have more confidence in yourself. Anyone would be lucky to have your food."
    show c flust surp
    show d ls
    "Cenric doesn't say anything and looks away. His face is almost completely red."
    hide c
    show l an at right, flip
    with dissolve
    voice l_sigh
    "I bite back a giggle and then turn to the food, grabbing some for myself. Lewis sighs and rolls his eyes."
    l "Anyway, back to the topic at hand."
    jump choice3_done

label choice3_appreciate: #WIP: Set transforms to show all 4 sprites on-screen at once
    show d a
    voice l_sigh
    "Lewis scoffs, rolling his eyes. I walk over to him and tug on his sleeve."
    show d n l
    mc "Come on."
    show l nerve
    l "[mcname]…"
    show d ls
    "I don't listen and drag him over to the cart. I point towards the food."
    show d fc
    mc "Go on. Choose one."
    show l an
    l "I don't want to."
    show d pout upset
    mc "As your fiance, I command you."
    show l ss
    voice l_laugh
    "He snorts and raises an eyebrow, crossing his arms."
    l "There's the [mcname] I'm used to."
    show l ss b
    l "Fine, I'll do it."
    show l neu
    "He looks over the food for a moment. He seems uninterested and grabs a small tart. He bites into it and then shrugs."
    l "It's all right. Nothing special."
    show d oh
    show c sad frown
    "I turn towards Cenric and see his face fall. I turn back towards Lewis and frown. Can someone really be this apathetic towards food?"
    hide c
    show d pout upset at left
    show l an at right, flip
    with moveoutleft
    mc "Don't be rude. Cenric worked hard."
    l "Food isn't necessarily something I need to worry about. We're here for a meeting, not your hunger."
    show d a
    "The words almost make me angry. I roll my eyes. This would be an issue to solve later."
    show d ss
    mc "Suit yourself."
    show o neu
    show o flust at center
    with dissolve
    "I smile towards Otillia and the grand mage."
    mc "Please, come eat."
    "Everyone shuffles in to get food. Lewis tries to keep the conversation on track."
    jump choice3_done

label choice3_done:
    scene bg tearoom day with fade
    show d ss at center with dissolve
    "We all sit with our plates, the conversation flowing easily between one another. The attention is on Otillia and her upbringing."
    "I never thought I'd be in a position like this. It seems so surreal that I'm in a pretty and glittering dress while talking to the two most important people in the kingdom."
    "At first, I was worried I would feel out of place. But I am genuinely having a good time. I feel like I have settled in, if only for now."
    show d gloom
    "I just need to avoid becoming a villainess."
    show o flust at left
    show l ss at right, flip
    with dissolve
    voice o3
    o "Well, I'm no one to celebrate."
    show d oh
    mc "What? Of course you are! You faced a hard childhood and look at you now. Studying directly under the grand mage. That deserves a party."
    show o blush smile
    voice o_laugh
    "Otillia giggles, covering her face."
    voice o4
    o "Oh, please. You and Prince Lewis have been the talk of the town since your engagement."
    show l bb n
    show d ss
    "Lewis's face turns red, and I smirk slightly."
    mc "(Right. Engaged.)"
    show o laugh
    voice o5
    o "You know, it would be great to have a ball!"
    show l nerve
    "Lewis looks at her curiously."
    voice l8
    l "A ball?"
    show o smile evil
    "She nods, a sudden glint in her eye."
    voice o6
    o "You two haven't had an engagement party, correct?"
    show d oh
    "I nod before glancing over at my fiance."
    mc "He isn't one for parties."
    show l an
    voice l9
    l "I'm too busy."
    show d pout upset
    mc "(And introverted.)"
    show d gloom
    "Like I'm one to talk. I spent my nights reading manhwa. Not exactly party animal material."
    show d ss
    show o happy ex
    voice o7
    o "How can you expect to be king if you can't organize balls?"
    show l nerve
    "Lewis opens his mouth then shuts it. Otillia's smile is bright as if she just won a prize."
    show o ce happy
    voice o8
    o "It's settled! I'll leave the planning to you two!"
    show o at offscreenleft
    show l neu at right, flip
    show d ss at left
    with move
    hide o
    "There isn't much room for discussion. Otillia quickly changes the subject and refuses to let it circle back to the party. After a while, both her and the grand mage depart."
    "I sit back in my chair, sipping on some tea. I think that went well. Well enough to form a good relationship with Otillia and not be blasted away by light magic."
    show d ls
    mc "(Victory!)"
    show l nerve
    l "[mcname]?"
    show d oh
    mc "Hmm?"
    "I look over at Lewis. He's staring straight at me as if he's studying a painting."
    voice l10
    l "I am truly concerned for you. You aren't acting like your normal self."
    show d ss
    mc "Would you rather I be cruel and rude?"
    voice l11
    l "No, but–"
    show d n l
    mc "Then there is no problem."
    show l an
    voice l_sigh
    "That shuts him up. He exhales, rubbing the bridge of his nose."
    voice l12
    l "You do know I have no time to be throwing parties. I am to be crowned king soon. I need to spend more time focusing on that."


    if CENRIC > LEWIS:
        jump CenricRoute
    else:
        jump LewisRoute

label CenricRoute:
    show d ls
    "I place my hands on my hips and grin."
    mc "Leave the party planning to me!"
    "Anyone can tell that Lewis is stressed. The last thing I want to do is add more stress to his daily duties. I want him to relax."
    mc "I'll coordinate with Cenric and the other castle staff. You can focus on your work."
    show l shock
    voice l18
    l "You'd do that?"
    mc "Of course. You'll only have to keep up appearances at the ball. No party planning."
    show l nerve
    "He thinks for a moment. It looks like he's having an internal battle with himself."
    "Finally, after what seems like an awkwardly long time, he speaks."
    show l ce
    voice l19
    l "All right. But if you run into any trouble at all, come straight to me. Is that clear?"
    show d n l
    mc "Yes, my prince."
    show l nerve
    voice l_sigh
    "He gives me a look. I giggle, continuing to sip my tea."
    show d fc
    "What he doesn't know is that I'll use this chance to eat more of Cenric's food."
    "It's a truly evil plan."
    jump cenric_route

label LewisRoute:
    show d ss
    mc "Then let me organize it."
    show l nerve
    voice l13
    l "Really?"
    show d ls
    "I nod. He scoffs as if it was a stupid joke."
    show l an
    voice l14
    l "I don't trust you to organize a ball all by yourself."
    show l ss
    "His voice softens just a bit."
    show d oh
    voice l15
    l "I will help."
    "I place my hand against my chin and tilt my head, humming slightly. I grin at him as an idea pops into my head."
    show d ls
    mc "Only if we figure out the menu together."
    show l shock
    voice l16
    l "That is the least important thing we need to figure out."
    show d pout upset
    "I gasp, offended at his words."
    mc "It's highly important! You don't want a party with bad food!"
    show l an
    show d n l
    "Lewis brushes off my words and stands up."
    voice l17
    l "Enough complaining. Let's go prepare."
    show d gloom
    "As he walks away, my face darkens. I will make Lewis see the true value of food."
    "An almost devious giggle leaves my mouth."
    show d fc
    mc "Evil plan, commence!"
    show d oh
    "I stop, a disgusted look crossing my face."
    show d gloom
    "Yeah. Not cut out to be a villainess."
    jump lewis_route
